plugins {
    id 'com.android.library'
    id 'de.undercouch.download' version "5.4.0"
}

ext {
    godotVersion = '4.0.2'
    godotRelease = 'stable'
    godotPluginName = 'MyGodotPlugin'
}

android {
    namespace 'com.fissuregames.godot.template'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    libraryVariants.all { variant ->
        task "copy${variant.name.capitalize()}Gdap" (type: Copy) {
            StringBuilder sb = new StringBuilder()
            sb.append("[config]\n")
            sb.append("name=\"${godotPluginName}_${variant.name}\"\n")
            sb.append("binary_type=\"local\"\n")
            sb.append("binary=\"${godotPluginName}.${variant.name}.aar\"\n")

            sb.append("[dependencies]\n")
            sb.append("remote=${ configurations.implementation.dependencies.collect {d -> "\"${d.group}:${d.name}:${d.version}\"" } }\n")
            new File(rootDir, "${godotPluginName}_${variant.name}.gdap").write(sb.toString())
        }
        variant.outputs.all { output ->
            output.outputFileName = "MyGodotPlugin.${variant.name}.aar"
        }
    }
}

task downloadLib(type: Download) {
    src "https://github.com/godotengine/godot/releases/download/${godotVersion}-${godotRelease}/godot-lib.${godotVersion}.${godotRelease}.template_release.aar"
    dest new File(projectDir, 'libs/godot-lib.aar')
}

task compile(dependsOn: downloadLib) {
    doLast {
        println "Downloaded godot-lib.aar"
    }
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: ['godot-lib.aar'])
}